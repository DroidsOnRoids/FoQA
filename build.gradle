buildscript {
    ext.kotlin_version = '1.3.21'

    ext.versions = [
            minSdk             : 15,
            compileSdk         : 28,
            targetSdk          : 28,
            kotlin             : '1.3.21',
            androidGradlePlugin: '3.3.1',
            mavenPublishPlugin : '0.7.0',
            versionsPlugin     : '0.21.0',
            hyperion           : '0.9.27',
            debugDb            : '1.0.5',
            hyperionChuck      : '1.0.0',
            hyperionAppInfo    : '1.0.0',
            hyperionSimpleItem : '0.4.0',
            deviceNames        : '1.1.8',
            autoService        : '1.0-rc4',
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:$versions.mavenPublishPlugin"
        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.versionsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects { subproject ->

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    apply plugin: 'com.github.ben-manes.versions'

    if (subproject.name == 'sample') {
        return
    }

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'com.vanniktech.maven.publish'

    android {
        compileSdkVersion versions.compileSdk

        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
        }

        resourcePrefix 'foqa_'
    }

    version = VERSION_NAME

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
        implementation "com.willowtreeapps.hyperion:hyperion-plugin:$versions.hyperion"
        kapt "com.google.auto.service:auto-service:$versions.autoService"
    }
}
