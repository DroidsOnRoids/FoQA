buildscript {
    ext.versions = [
            minSdk             : 15,
            compileSdk         : 29,
            targetSdk          : 29,
            kotlin             : '1.3.41',
            androidGradlePlugin: '3.4.2',
            versionsPlugin     : '0.22.0',
            hyperion           : '0.9.27',
            debugDb            : '1.0.6',
            chucker            : '3.0.0-beta1',
            hyperionAppInfo    : '1.1.0',
            deviceNames        : '1.1.9',
            autoService        : '1.0-rc6',
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.versionsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.koral--:shipkit:5406deb9d1746f963d1b132bccb3cca17021c3e9' //TODO replace with parent shipkit project once Android support is ready
    }
}

apply plugin: 'org.shipkit.java'

subprojects { subproject ->

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'com.github.ben-manes.versions'
    group = 'pl.droidsonroids.foqa'

    if (subproject.name == 'sample') {
        return
    }

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'org.shipkit.bintray'

    apply plugin: 'org.shipkit.android-publish'

    android {
        compileSdkVersion versions.compileSdk

        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
        }

        resourcePrefix 'foqa_'
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
        implementation "com.willowtreeapps.hyperion:hyperion-plugin:$versions.hyperion"
        kapt "com.google.auto.service:auto-service:$versions.autoService"
    }
}
