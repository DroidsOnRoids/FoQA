buildscript {
    ext.versions = [
            minSdk             : 15,
            compileSdk         : 29,
            targetSdk          : 29,
            kotlin             : '1.3.50',
            androidGradlePlugin: '3.6.0-beta01',
            versionsPlugin     : '0.25.0',
            hyperion           : '0.9.27',
            debugDb            : '1.0.6',
            chucker            : '3.0.1',
            hyperionAppInfo    : '1.1.0',
            deviceNames        : '1.1.9',
            autoService        : '1.0-rc6',
            shipkit            : '5406deb9d1746f963d1b132bccb3cca17021c3e9',
            detekt             : '1.1.0'
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradlePlugin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.versionsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.koral--:shipkit:$versions.shipkit"
        //TODO replace with parent shipkit project once Android support is ready
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version "1.1.0"
    id 'com.gradle.build-scan' version '2.4.2'
}

apply plugin: 'org.shipkit.java'

dependencies {
    detekt "io.gitlab.arturbosch.detekt:detekt-formatting:$versions.detekt"
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$versions.detekt"
}

repositories {
    mavenCentral()
    jcenter()
}

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
}

detekt {
    input = files(projectDir)
    config = rootProject.files("detekt-config.yml")
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    exclude("build/")
    exclude("buildSrc/build/")
    parallel = true
    reports {
        xml.enabled = true
        html.enabled = true
        txt.enabled = false
    }
}

subprojects { subproject ->

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'com.github.ben-manes.versions'
    group = 'pl.droidsonroids.foqa'

    if (subproject.name == 'sample') {
        return
    }

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'org.shipkit.bintray'

    apply plugin: 'org.shipkit.android-publish'

    android {
        compileSdkVersion versions.compileSdk

        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
        }

        resourcePrefix 'foqa_'

        libraryVariants.all {
            // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
            generateBuildConfigProvider.configure {
                enabled false
            }
        }
        tasks.withType(JavaCompile) {
            enabled false
        }
        variantFilter {
            if (it.name == "debug") {
                setIgnore(true)
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
        implementation "com.willowtreeapps.hyperion:hyperion-plugin:$versions.hyperion"
        kapt "com.google.auto.service:auto-service:$versions.autoService"
    }
}
